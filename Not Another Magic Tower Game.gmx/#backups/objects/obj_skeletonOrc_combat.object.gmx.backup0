// 2025-06-08 11:01:16
#event properties (no comments/etc. here are saved)
parent_index = obj_monsterBattlePrototype;
sprite_index = spr_orcSkeleton_combat;
uses_physics = false;

#event create
event_inherited();
name = "Skeleton Orc";
image_speed = 1/8;
x = 752;
y = 560;
step = 0;
path[0] = pathSkeletonOrc1;
path[1] = pathSkeletonOrc2;
path[2] = pathSkeletonOrc3;
path[3] = pathSkeletonOrc4;
path[4] = pathSkeletonOrc5;
alarm[11] = 15;

circleCount = 0;
viewSplitReady = false;
viewSplit = false;
endPath = false;

#event alarm3
instance_create(x - 4, y, obj_skeletonOrcSwordHit);
alarm[11] = 70;

#event alarm4
if(circleCount > 0)
{
    if(circleCount == 15)
    {
        instance_create(x - 4, y, obj_skeletonOrcSwordHit2);
    }
    if(circleCount mod 3 == 0)
    {
        sound_fix(snd_touhou_attack3);
    }
    var b = instance_create(x - 4 + 80 *cos(degtorad((15 - circleCount) * 13 + 70)), y - 80 * sin(degtorad((15 - circleCount) * 13 + 70)), obj_skeletonOrcSwordBullet1);
    b.image_angle = point_direction(x - 4, y, b.x, b.y) - 90;
    b.dir = point_direction(x - 4, y, b.x, b.y);
    circleCount -= 1;
    alarm[4] = 1;
}
else
{
    with(obj_skeletonOrcSwordBullet1)
    {
        if(!start)
        {
            alarm[3] = 1;
        }
    }
    alarm[11] = 20;
}

#event alarm5
instance_create(x - 10, y, obj_skeletonOrcBoomerang);
alarm[11] = 50;

#event alarm10
circleCount = 15;
alarm[choose(3,4,5)] = 10;

#event alarm11
if(viewSplitReady && !viewSplit)
{
    exit;
}
path_start(path[step],6,0,true);
endPath = false;

#event step
if(!viewSplitReady && hp <= 40 && endPath)
{
    viewSplitReady = true;
    instance_create(400,304,obj_skeletonOrcViewSpliter);
}

#event collision:bullet
if(!viewSplit && hp <= 40)
{
    with(other)
    {
        instance_destroy();
    }   
    if(!instance_exists(obj_monsterHPBlock))
    {
        instance_create(x,y,obj_monsterHPBlock);
    }
    audio_playsound(sndIce1);
}
else
{
    event_inherited();
}

#event other_end_of_path
if(step == 4)
{
    step = 0;
}
else
{
    step += 1;
}
endPath = true;
alarm[choose(10,10,10,11)] = 1;

#event other_user15
if(player.x > 450)
{
    if(!global.achievements[70])
    {
        var ch = instance_create(0,608,obj_achievements);
        ch.index = 70;
        global.achievements[70] = 1;
    }
}