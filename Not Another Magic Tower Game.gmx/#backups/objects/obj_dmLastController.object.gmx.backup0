// 2025-06-08 12:20:56
#event properties (no comments/etc. here are saved)
parent_index = -1;
uses_physics = false;

#event create
alarm[0] = 50;


dis = 0;

#event alarm0
with(obj_darkMage_combat2)
{
    if(point_distance(x,y,400,288) > 8)
    {
        move_towards_point(400,288,8);
        other.alarm[0] = 1;
    }
    else
    {
        x = 400;
        y = 288;
        speed = 0;
        other.alarm[1] = 30;
    }
}

#event alarm1
dis = 256;
instance_create(400 - 256, 288, obj_dmWeaponAppearEffect);
instance_create(400 - 256, 288, obj_dmSword2);
instance_create(400 + 256, 288, obj_dmWeaponAppearEffect);
instance_create(400 + 256, 288, obj_dmSickle);
sound_fix(sndFire1);

#event alarm2
sound_fix(sndExpMax);
instance_create(x,y,obj_levelupFlash);
instance_create(x,y,obj_dmSwordAndSickle);

#event alarm3
if(instance_exists(obj_dmSwordAndSickle))
{
    if(point_distance(obj_darkMage_combat2.x, obj_darkMage_combat2.y, 400, 288) < 256)
    {
        skill = choose(0,1);   
    }
    else
    {
        skill = 0;
    }
    
    if(skill == 0)
    {
        with(obj_darkMage_combat2)
        {
            reached = false;
            targetX = player.x;
            targetY = player.y;
        }
        alarm[4] = 1;
    }
    else if(skill == 1)
    {    
        alarm[5] = 1;
    }
}
else
{
    alarm[10] = 1;
}

#event alarm4
with(obj_darkMage_combat2)
{
    if(point_distance(x,y,targetX,targetY) > 10 && !reached)
    {
        move_towards_point(targetX, targetY, 10);   
        other.alarm[4] = 1;
    }
    else
    {
        if(!reached)
        {
            reached = true;
            obj_dmSwordAndSickle.alarm[3] = 1;
            haki = instance_create(obj_dmSwordAndSickle.x, obj_dmSwordAndSickle.y, obj_dmSwordHaki);
            if(obj_dmSwordAndSickle.image_index == 0)
            {
                with(haki)
                {
                    event_user(0);
                }
            }
        }
        if(speed > 0)
        {
            speed -= 0.4;
            other.alarm[4] = 1;
        }
        else
        {
            speed = 0;
            other.alarm[3] = 50;
        }
    }
}

#event alarm5
with(obj_dmSwordAndSickle)
{
    status = 2;
    startAngle = image_angle;
    alarm[5] = 1;
    alarm[6] = 50;
}


#event alarm10
with(obj_darkMage_combat2)
{
    if(y > -96)
    {
        vspeed = -8;
        other.alarm[10] = 1;
    }
    else
    {
        vspeed = 0;
        x = 400;
        other.alarm[11] = 20;
    }
}

#event alarm11
with(obj_darkMage_combat2)
{
    if(y < 128)
    {
        vspeed = 8;
        other.alarm[11] = 1;
    }
    else
    {
        vspeed = 0;
        shotGunCount = 30;
        alarm[6] = 50;
        with(other)
        {
            instance_destroy();
        }
    }
}
