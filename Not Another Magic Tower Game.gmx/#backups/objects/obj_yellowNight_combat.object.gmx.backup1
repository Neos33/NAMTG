// 2025-06-05 09:12:49
#event properties (no comments/etc. here are saved)
parent_index = obj_monsterBattlePrototype;
sprite_index = spr_yellowKnight;
uses_physics = false;

#event create
event_inherited();
name = "Yellow Knight";
alarm[3] = 20;
phase = "";
image_speed = 1/8;


dCount = 0;
targetX = 0;
targetY = 0;
spd = 0;

centerX = 0;
centerY = 0;
num = 0;

isAch = 0;
angle = 0;
fAngle = 0;
mCount = 0;
mTime = 0;

hit = 0;
p_dis = 0;
p_dir = 0;
tx = 0;
ty = 0;


cPhase = 0;
cCount1 = 0;
cCount2 = 0;
cCount3 = 0;


backupPhase = 2;
cInterval = 50;
cTime = 0;

#event alarm3
phase = "dash";
dCount += 1;
targetX = x - 200;
targetY = y;
spd = 10;

#event alarm4
audio_playsound(sndSkill1);
alarm[5] = 30;

#event alarm5
centerX = 400;
centerY = 304;
dCount = 0;
phase = "magic";
num = choose(5,7,9);
if(num == 9)
{
    isAch = true;
}
angle = 360 / num;
fAngle = -angle;
x = centerX + 150 * cos(degtorad(fAngle)) - 16;
y = centerY - 150 * sin(degtorad(fAngle)) - 16;
fAngle += 2 * angle;
mCount = 1;
mTime = 0;
targetX = centerX + 150 * cos(degtorad(fAngle)) - 16;
targetY = centerY - 150 * sin(degtorad(fAngle)) - 16;
spd = 24;

#event alarm6
hit = instance_create(x + 2, y + 16, obj_swordhit);
hit.image_xscale = -1;
audio_playsound(sndSkill1);
p_dis = point_distance(x,y,400,304);
p_dir = point_direction(400,304,x,y);
tx = 400 + p_dis * 0.75 * cos(degtorad(p_dir));
ty = 304 - p_dis * 0.75 * sin(degtorad(p_dir));
with(obj_yellowKnightBullet1)
{
    speed = 8;
    direction = point_direction(x,y, other.tx, other.ty);
}
alarm[7]=30;

#event alarm7
phase = "combo";
x = 704;
y = 544;
mTime = 0;
mCount = 0;
cPhase = 1;
cCount1 = 0;
cCount2 = 0;
cCount3 = 0;
hit = instance_create(x + 2, y + 16, obj_swordhit);
hit.image_xscale = -1;

#event alarm10
if(cCount1 < 10)
{
    with(combo1[cCount1])
    {
        speed = 15;
        direction = point_direction(x,y, player.x, player.y);
    }
    cCount1 += 1;
    alarm[10] = 10;
}
else
{
    cPhase = 4;
    backupPhase = 2;
    cInterval = 50;
    cTime = 0;
}

#event step Phase: Dash
if(phase == "dash")
{
    if(point_distance(x,y,targetX,targetY) < spd)
    {
        hspeed = 0;
        vspeed = 0;
        sprite_index = spr_yellowKnight;
        hit = instance_create(x + 2, y + 16, obj_swordhit);
        hit.image_xscale = -1;
        phase = "init";
        if(dCount < 3)
        {
            alarm[3] = 20;
        }
        else
        {
            alarm[4] = 20;
        }
    }
    else
    {
        move_towards_point(targetX, targetY, spd);
    }
}

#section Phase: Magic
if(phase == "magic")
{
    mTime +=1;
    if(mTime mod 2 == 0)
    {
        audio_playsound(sndShoot);
    }
    instance_create(x + 16,y + 16, obj_yellowKnightBullet1);        
    if(point_distance(x, y, targetX, targetY) < spd)
    {
        hspeed = 0;
        vspeed = 0;
        if(mCount < num)
        {
            mCount += 1;
            fAngle += 2* angle;
            targetX = centerX + 150 * cos(degtorad(fAngle)) - 16;
            targetY = centerY - 150 * sin(degtorad(fAngle)) - 16;
        }
        else
        {
            x = 400 - 16;
            y = 304 - 16;;
            phase = "init";
            alarm[6] = 10;
        }
    }
    else
    {
        move_towards_point(targetX, targetY, spd);
    }
}
#section Phase: Combo
if(phase == "combo")
{
    if(cPhase == 1)
    {
        if(cCount1 < 10)
        {
            audio_playsound(sndShoot);
            combo1[cCount1] = instance_create(x - 15, y - 20 + cCount1 * 4, obj_yellowKnightBullet1);
            cCount1 += 1;
        }
        else
        {
            cPhase = 0;
            cCount1 = 0;
            alarm[10] = 20;
        }
    }
    else if(cPhase == 2)
    {
        if(cCount2 < 200)
        {
            if(cCount2 mod 2 == 0)
            {
                audio_playsound(sndShoot);
                c2_1 = instance_create(x - 15, y + 48 - 24 +  abs(5 - ((cCount2 / 2) mod 10)) * 8, obj_yellowKnightBullet2);
                c2_2 = instance_create(x - 15, y - 48 - 24 - abs(5 - ((cCount2 / 2) mod 10)) * 8, obj_yellowKnightBullet2);
                c2_1.hspeed = -8;
                c2_2.hspeed = -8;
            }
            cCount2 += 1;
        }
        else
        {
            cPhase = 4;
            backupPhase = 3;
            cInterval = 50;
            cTime = 0;
        }
    }
    else if(cPhase == 3)
    {
        if(cCount3 < 200)
        {
            if(cCount2 mod 2 == 0)
            {
                audio_playsound(sndShoot);
                var c3_1 = instance_create(x - 15, y + 24, obj_yellowKnightBullet3);
                var c3_2 = instance_create(x - 15, y - 24, obj_yellowKnightBullet3);
                c3_1.type = 0;
                c3_2.type = 1;
                c3_1.vspeed = -5.25;
                c3_2.vspeed = 5.25;
                c3_1.hspeed = -5;
                c3_2.hspeed = -5;
            }           
        }
        else if(cCount3 == 250)
        {
            alarm[3] = 1;
        }
        cCount3 += 1;
    }
    else if(cPhase == 4)
    {
        if(cTime < cInterval)
        {
            cTime += 1;
        }       
        else
        {
            cPhase = backupPhase;
            hit = instance_create(x + 2, y + 16, obj_swordhit);
            hit.image_xscale = -1;
            audio_playsound(sndSkill1);
        }
    }
}

#event other_user15
if(isAch && global.playerHP == global.hp)
{
    if(!global.achievements[34])
    {
        var ch = instance_create(0,608,obj_achievements);
        ch.index = 34;
        global.achievements[34] = true;
    }
}